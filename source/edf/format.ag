guard PaddedBase64URL: string("^([A-Za-z0-9-_]{4})*([A-Za-z0-9-_]{3}[=]{1}|[A-Za-z0-9-_]{2}[=]{2})?$");

guard NonNegativeNumber: number(0, *);

guard PositiveInteger: integer(1, *);

guard ColorComponent: number(0, 1);

guard GrayscaleColor: {
	i: ColorComponent
};

guard RGBColor: {
	r: ColorComponent,
	g: ColorComponent,
	b: ColorComponent
};

guard CMYKColor: {
	c: ColorComponent,
	m: ColorComponent,
	y: ColorComponent,
	k: ColorComponent
};

guard Color: GrayscaleColor | RGBColor | CMYKColor;

guard AbsoluteUnit: "mm" | "pt" | "in" | "cm" | "pc";

guard Length: NonNegativeNumber | [NonNegativeNumber] | [NonNegativeNumber, "%"];
guard NodeLength: Length | [NonNegativeNumber, "fr"] | "intrinsic" | "extrinsic";

guard Size: {
	w: NonNegativeNumber,
	h: NonNegativeNumber
};

guard Style: {
	template?: string
};

guard NodeStyle: {
	height?: NodeLength,
	overflow?: "hidden" | "visible",
	segmentation?: "auto" | "none",
	width?: NodeLength
};

guard Node: {
	type: string
};

guard ChildNode: Node & {

};

guard ParentNode: ChildNode & {
	children: Node[]
};

guard TextStyle: {
	color?: string | "transparent" | Color,
	columns?: PositiveInteger,
	font?: string,
	font_size?: NonNegativeNumber,
	gutter?: Length,
	letter_spacing?: NonNegativeNumber,
	line_anchor?: "meanline" | "capline" | "topline" | "bottomline" | "baseline",
	line_height?: NonNegativeNumber,
	orphans?: PositiveInteger,
	text_align?: "start" | "center" | "end",
	text_transform?: "none" | "lowercase" | "uppercase",
	white_space?: "wrap" | "nowrap",
	word_spacing?: NonNegativeNumber
};

guard TextNodeStyle: Style & NodeStyle & TextStyle;

guard TextNode: ChildNode & {
	type: "text",
	content: string,
	style?: TextNodeStyle
};

guard BoxStyle: {
	align_x?: "left" | "center" | "right",
	align_y?: "top" | "middle" | "bottom",
	background_color?: string | "transparent" | Color,
	border_color?: string | "transparent" | Color,
	border_radius?: Length,
	border_width?: Length,
	gap?: Length,
	layout?: "vertical" | "horizontal",
	padding?: Length
};

guard BoxNodeStyle: Style & NodeStyle & BoxStyle;

guard BoxNode: ParentNode & {
	type: "box",
	style?: BoxNodeStyle
};

guard Colors: {
	Color
};

guard Metadata: {
	title?: string,
	author?: string
};

guard Templates: {
	box?: { BoxNodeStyle },
	text?: { TextNodeStyle }
};

guard Document: {
	colors?: Colors,
	content: Node,
	files?: { PaddedBase64URL },
	fonts?: { string },
	metadata?: Metadata,
	size: Size,
	templates?: Templates
};
