guard Base64: string("^([A-Za-z0-9+/]{3})*([A-Za-z0-9+/]{2}[=]{1}|[A-Za-z0-9+/]{1}[=]{2})?$");

guard NonNegativeNumber: number(0, *);

guard PositiveInteger: integer(1, *);

guard ColorComponent: number(0, 1);

guard GrayscaleColor: {
	i: ColorComponent
};

guard RGBColor: {
	r: ColorComponent,
	g: ColorComponent,
	b: ColorComponent
};

guard CMYKColor: {
	c: ColorComponent,
	m: ColorComponent,
	y: ColorComponent,
	k: ColorComponent
};

guard Color: GrayscaleColor | RGBColor | CMYKColor;

guard Length: NonNegativeNumber | [NonNegativeNumber] | [NonNegativeNumber, "%"];
guard NodeLength: Length | [NonNegativeNumber, "fr"] | "intrinsic" | "extrinsic";

guard Size: {
	w: NonNegativeNumber,
	h: NonNegativeNumber
};

guard Style: {
	template?: string
};

guard NodeStyle: {
	height?: NodeLength,
	overflow?: "hidden" | "visible",
	segmentation?: "auto" | "none",
	width?: NodeLength
};

guard Node: {
	type: string
};

guard ChildNode: Node & {

};

guard ParentNode: ChildNode & {
	children: Node[]
};

guard TextStyle: {
	color?: "transparent" | Color,
	columns?: PositiveInteger,
	font_size?: NonNegativeNumber,
	gutter?: Length,
	letter_spacing?: NonNegativeNumber,
	line_anchor?: "meanline" | "capline" | "topline" | "bottomline" | "baseline",
	line_height?: NonNegativeNumber,
	orphans?: PositiveInteger,
	text_align?: "start" | "center" | "end",
	text_transform?: "none" | "lowercase" | "uppercase",
	white_space?: "wrap" | "nowrap",
	word_spacing?: NonNegativeNumber
};

guard TextNodeStyle: Style & NodeStyle & TextStyle;

guard TextNode: ChildNode & {
	type: "text",
	content: string,
	font: string,
	style?: TextNodeStyle
};

guard BoxStyle: {
	background_color?: "transparent" | Color,
	border_color?: "transparent" | Color,
	border_radius?: Length,
	border_width?: Length,
	padding?: Length
};

guard BoxNodeStyle: Style & NodeStyle & BoxStyle;

guard BoxNode: ParentNode & {
	type: "box",
	style?: BoxNodeStyle
};

guard VerticalStyle: {
	align_x?: "left" | "center" | "right",
	align_y?: "top" | "middle" | "bottom",
	gap?: Length
};

guard VerticalNodeStyle: Style & NodeStyle & VerticalStyle;

guard VerticalNode: ParentNode & {
	type: "vertical",
	style?: VerticalNodeStyle
};

guard HorizontalStyle: {
	align_x?: "left" | "center" | "right",
	align_y?: "top" | "middle" | "bottom",
	gap?: Length
};

guard HorizontalNodeStyle: Style & NodeStyle & HorizontalStyle;

guard HorizontalNode: ParentNode & {
	type: "horizontal",
	style?: HorizontalNodeStyle
};

guard Templates: {
	box?: { BoxNodeStyle },
	horizontal?: { HorizontalNodeStyle },
	text?: { TextNodeStyle },
	vertical?: { VerticalNodeStyle }
};

guard Document: {
	content: Node,
	files?: { Base64 },
	fonts?: { string },
	size: Size,
	templates?: Templates
};
